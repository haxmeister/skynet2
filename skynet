#!/usr/bin/perl

use v5.36;

use FindBin;
use lib "$FindBin::Bin/lib";
use IO::Async::Loop;
use IO::Async::Listener;
use Skynet::Manager;
use Skynet::User;
use Data::Dumper;
use Config::INI::Reader;
use DBI;

my $config = Config::INI::Reader->read_file('/etc/skynet.ini');

my $db = DBI->connect_cached(
    "dbi:mysql:".$config->{database}{name},
    $config->{database}{username},
    $config->{database}{password},
    {
        RaiseError => 1,
        AutoCommit => 1,
        mysql_auto_reconnect => 1,
    },
) or die "cannot connect to database: ".$DBI::errstr;

my $loop = IO::Async::Loop->new;
my $manager = Skynet::Manager->new(notifier_name => 'manager');
$manager->db($db);

my $listener = IO::Async::Listener->new(
    on_accept => sub{
        my (undef, $socket) = @_;
        my $newUser = Skynet::User->new(
            handle => $socket,
        );
        say "new user connected at ".$socket->peerhost . ":" . $socket->peerport;
        $newUser->username($socket->peerhost . ":" . $socket->peerport);
        $manager->add_to_guild($newUser, 'lobby');
    },
);

$loop->add($manager);
$loop->add($listener);

$listener->listen(
   service  => $config->{server}{port},
   socktype => 'stream',
)->get;

say "Listening on port ".$config->{server}{port};
$loop->run;
